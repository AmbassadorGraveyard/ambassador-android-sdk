apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    flatDir {
        dirs '../ambassadorsdk/libs'
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ambassador.app"
        minSdkVersion 16
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 5
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

apply from: file('dependencies.gradle')

configurations {
    androidTestCompile.exclude group: 'org.hamcrest', module: 'hamcrest-library'
    androidTestCompile.exclude group: 'org.hamcrest', module: 'hamcrest-integration'
    androidTestCompile.exclude group: 'org.hamcrest', module: 'hamcrest-core'
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-annotations:23.0.1'
    }
}

dependencies {
    compile project(':ambassadorsdk')

    rootProject.ext.demoCompile.each {
        compile "${it}"
    }

    rootProject.ext.demoProvided.each {
        provided "${it}"
    }

    rootProject.ext.demoTestCompile.each {
        testCompile "${it}"
    }

    rootProject.ext.demoTestApt.each {
        testApt "${it}"
    }


    rootProject.ext.demoAndroidTestCompile.each {
        androidTestCompile "${it}"
    }

    rootProject.ext.demoAndroidTestApt.each {
        androidTestApt "${it}"
    }

    compile 'asia.ivity.android:drag-sort-listview:1.0'
    compile 'com.squareup:javapoet:1.6.1'
}




